plugins {
    id "org.jetbrains.intellij" version "0.2.7"
}

repositories {
    maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }

    mavenCentral()
}

version = "${new File('pluginVersion.txt').getText('UTF-8')}.${ideaBranch}"

allprojects {
    apply plugin: 'java'
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    plugins.withType(JavaPlugin) {
        test {
            systemProperty "NO_FS_ROOTS_ACCESS_CHECK", "true"
        }
    }

    sourceSets {
        main {
            java.srcDirs 'src'
            resources.srcDir 'resources'
        }
        test {
            java.srcDir 'test'
            resources.srcDir 'testResources'
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        version ideaVersion
        type "IC"
        pluginName 'BashSupport'
        plugins 'IntelliLang'
        downloadSources Boolean.valueOf(sources)
    }

    publishPlugin {
        username 'wallaby'
        password System.properties['intellij.publish.password']
        channels "eap"
    }

    apply plugin: 'jacoco'
    jacoco {
        toolVersion = "0.7.6.201602180812"
    }

    test {
        jacoco {
            includes ["com.ansorgit.*"]
        }
    }

    ant.importBuild("build.xml")
    task doJFlex(dependsOn: JavaPlugin.PROCESS_RESOURCES_TASK_NAME) {
        doLast {
            jflex
        }
    }

    compileJava.dependsOn(doJFlex)
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
